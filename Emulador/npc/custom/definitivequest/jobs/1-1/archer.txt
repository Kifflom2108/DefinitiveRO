payon_in02,64,71,4	script	Mestre Da Guilda#archer	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "[Mestre Da Guilda]";
			mes "Ei, eu te conheço!";
			mes "Você já fez esse teste antes, não?";

			next;
			mes "[Mestre Da Guilda]";
			mes "Ah, você deve ter ido para Valhalla e renascido.";
			mes "Uau, isso é impressionante!";

			next;
			if (!callfunc("F_CanChangeJob")) {
				mes "[Mestre Da Guilda]";
				mes "Err...";
				mes "É melhor você aprender todas as Habilidades Básicas antes de se tornar um Arqueiro.";

				next;
				mes "[Mestre Da Guilda]";
				mes "Certo, até mais.";
				close;
			}
			mes "[Mestre Da Guilda]";
			mes "Bem então. Eu não";
			mes "preciso dizer mais nada.";
			mes "Eu sei que você será um grande Arqueiro...";
			next;
			skill "NV_TRICKDEAD",0,SKILL_PERM;
			jobchange Job_Archer_high;
			skill "AC_MAKINGARROW",1,SKILL_PERM;
			skill "AC_CHARGEARROW",1,SKILL_PERM;
			mes "[Mestre Da Guilda]";
			mes "Embora não haja uma";
			mes "recompensa especial para você desta vez, espero que entenda.";
			mes " Cuide-se.";
			close;
		}
		else {
			mes "Oh...?";
			mes "Ei, o que você está";
			mes "fazendo aqui...?";
			next;
			mes "[Mestre Da Guilda]";
			mes "Eu posso perceber que você não tem o perfil para ser um Arqueiro."; 
			mes "Parece que você foi feito para fazer";
			mes "algo diferente...";
		}
	}
	mes "[Mestre Da Guilda]";
	mes "Bom dia. Como posso ajudá-lo?";
	next;
	switch(select("Quero ser um Arqueiro.:Preciso dos requisitos, por favor.:Nada, obrigado.")) {
	case 1:
		if (BaseJob == Job_Archer) {
			mes "[Mestre Da Guilda]";
			mes "Você já se tornou um Arqueiro...";
			close;
		}
		else if (BaseJob != Job_Archer && BaseJob != Job_Novice) {
			mes "[Mestre Da Guilda]";
			mes "Hmm...";
			mes "Você não parece nem um pouco com um Aprendiz...";
			next;
			mes "[Mestre Da Guilda]";
			mes "De qualquer forma, Seja lá o que você for, não pode escolher o trabalho de Arqueiro.";
			close;
		}
		if (job_archer_q == 0) {
			mes "[Mestre Da Guilda]";
			mes "Você quer ser um Arqueiro?";
			mes "Se sim, você precisa preencher este formulário de inscrição.";
			next;
			if (select("Inscrever-se.:Cancelar") == 1) {
				set job_archer_q,1;
				setquest 1004;
				mes "[Mestre Da Guilda]";
				mes "Certo, assine aqui. Bem, eu vou promovê-lo assim que você atender aos requisitos.";
				next;
				mes "[Mestre Da Guilda]";
				mes "Se você acha que já os atendeu, podemos verificar isso agora.";
				mes "Está pronto?";
				next;
				if (select("Sim, estou pronto.:Não, ainda não.") == 1) {
					mes "[Mestre Da Guilda]";
					mes "Certo, deixa-me verificar.";
					next;
				}
				else {
					mes "[Mestre Da Guilda]";
					mes "Entendo. Fique à vontade para olhar os requisitos, se quiser.";
					close;
				}
			}
			else {
				mes "[Mestre Da Guilda]";
				mes "Bem, tudo bem.";
				mes "Até a próxima.";
				close;
			}
		}
		mes "[Mestre Da Guilda]";
		mes "Você é... " + strcharinfo(0) + "?";
		next;
		if (!callfunc("F_CanChangeJob")) {
			mes "[Mestre Da Guilda]";
			mes "Bem, você não está no nível de classe adequado. Por favor, verifique os requisitos novamente.";
			next;
			mes "[Mestre Da Guilda]";
			mes "Seu nível de classe deve ser pelo menos 10, e não se esqueça de aprender todas as Habilidades Básicas. Quando tiver feito isso, volte aqui.";
			close;
		}
		if (job_archer_q == 1) {
			set .@archer_item1,countitem(1066) * 5;
			set .@archer_item2,countitem(1067) * 3;
			set .@archer_item3,countitem(1068) * 2;
			set .@archer_item4,countitem(1019);
			set .@total_archer,.@archer_item1 + .@archer_item2 + .@archer_item3 + .@archer_item4;
			set .@total_archer2,(((.@archer_item2 + .@archer_item3) * 2) + .@archer_item4);
			mes "[Mestre Da Guilda]";
			mes "Excelente!";
			mes "Agora então,";
			mes "vamos ver...";
			next;
			mes "[Mestre Da Guilda]";
			mes "Vou avaliar os diferentes tipos de Troncos que você trouxe, necessários para produzir um Arco.";
			next;
			if (countitem(1066) == 0 && countitem(1067) == 0 && countitem(1068) == 0 && countitem(1019) == 0) {
				mes "[Mestre Da Guilda]";
				mes "Hmm...";
				mes "Infelizmente, você não trouxe nenhum dos itens necessários.";
				mes "Não há nada para avaliar.";
				navigateto "payon_in02", 26, 67;
				close;
			}
			mes "[Mestre Da Guilda]";
			if (countitem(1066) != 0) mes "Grau S : " + countitem(1066) + " un, Pontos : " + .@archer_item1 + " . ";
			if (countitem(1067) != 0) mes "Grau A : " + countitem(1067) + " un, Pontos : " + .@archer_item2 + " . ";
			if (countitem(1068) != 0) mes "Grau B : " + countitem(1068) + " un, Pontos : " + .@archer_item3 + " . ";
			if (countitem(1019) != 0) mes "Grau C : " + countitem(1019) + " un, Pontos : " + .@archer_item4 + " . ";
			if (.@total_archer < 100) {
				mes "Total de Pontos: ^FF0000" + .@total_archer + "^000000 / 100";
				next;
				mes "[Mestre Da Guilda]";
				mes "Você precisa de pelo menos 100 pontos!.";
				mes "Vamos, tente mais um pouco!";
				emotion ET_GO;
				navigateto "payon_in02", 26, 67;
				close;
			}
			else {
				mes "Total de Pontos: ^0000FF" + .@total_archer + "^000000 / 100";
				next;
				mes "[Mestre Da Guilda]";
				if (.@total_archer > 150) {
					mes "Uau! Mais de 150 pontos!";
					mes "Excelente! Parabéns!";
				}
				else if (.@total_archer > 120) {
					mes "Mais de 120 pontos! Bom trabalho!";
					mes "Parabéns!";
				}
				else {
					mes "*Suspiro* Bem, você passou por pouco... Enfim, bem feito.";
				}
			}
			next;
			mes "[Mestre Da Guilda]";
			mes "Vou transferir esses Troncos para o nosso Departamento de Produção de Arcos.";
			mes "Agora que você atendeu aos requisitos, deixe-me promovê-lo imediatamente!";
			if (countitem(1066) != 0) delitem 1066,countitem(1066);
			if (countitem(1067) != 0) delitem 1067,countitem(1067);
			if (countitem(1068) != 0) delitem 1068,countitem(1068);
			if (countitem(1019) != 0) delitem 1019,countitem(1019);
		}
	next;
	callfunc "Job_Change",Job_Archer;
	callfunc "F_ClearJobVar";
	completequest 1004;
	mes "[Mestre Da Guilda]";
	mes "Parabéns!";
	mes "Agora você é um Arqueiro!";
	next;
	mes "[Mestre Da Guilda]";
	mes "Claro, esperamos que você contribua para o futuro da Guilda dos Arqueiros com seus esforços.";
	next;
	mes "[Mestre Da Guilda]";
	mes "Ah, seu arco chegou do Departamento de Produção de Arcos.";
	mes "Aqui, pegue-o! É seu";
	getitem 1703,1;
	getitem 1750,.@total_archer2;
	next;
	mes "[Mestre Da Guilda]";
	mes "Agora inicie o treinamento.";
	mes "Converse com o instrutor arqueiro na sala ao lado.";
	navigateto "payon_in02", 22, 31;
	setquest 75001;
	close;
	case 2:
	mes "[Mestre Da Guilda]";
	mes "Vou explicar os requisitos para ser um Arqueiro.";
	if (BaseJob != Job_Novice) {
		if (BaseJob == Job_Archer) {
			next;
			mes "[Mestre Da Guilda]";
			mes "Mas...";
			mes "Você já é um Arqueiro.";
			mes "Já deve saber disso...";
			next;
		}
		else {
			next;
			mes "[Mestre Da Guilda]";
			mes "Espere um segundo.";
			mes "Você já escolheu outra classe.";
			mes "Não precisa saber disso";
			close;
		}
		mes "[Mestre Da Guilda]";
		mes "Então... Não há realmente motivo para eu te contar os requisitos...";
		close;
	}
	next;
	mes "[Mestre Da Guilda]";
	mes "Primeiro de tudo, você precisa ser nível 10 de classe como Aprendiz e ter aprendido todas as Habilidades Básicas.";
	next;
	mes "[Mestre Da Guilda]";
	mes "Um Arqueiro precisa de concentração e reflexos extremamente altos, então não aceitamos aqueles que têm pouca paciência.";
	next;
	mes "[Mestre Da Guilda]";
	mes "Você também precisa coletar ^FF0000Troncos^000000.";
	mes "Existem 4 tipos diferentes de Troncos, cada um com uma qualidade diferente.";
	mes "Você receberá diferentes Pontos para seus Troncos, dependendo de sua qualidade.";
	next;
	mes "[Mestre Da Guilda]";
	mes "Para se tornar um Arqueiro, você deve receber pelo menos ^0000FF25^000000 pontos de 100.";
	mes "Você pode pegar Troncos na árvore 'Willow'.";
	mes "Tenha cuidado, pois são monstros difíceis.";
	close;
	case 3:
	close;
	}
}

payon_in02,22,31,4	script	Instrutor#archer	732,{
	if (BaseJob == Job_Archer){
		if (checkquest (75001) == 1){
		completequest 75001;
		}
		if (checkquest(75002) == -1) {
            mes "[Instrutor]";
            mes "Você é novo por aqui, não é?";
            mes "Bom, antes de empunhar um arco com propósito, precisa entender o que significa caçar.";
            mes "Não se trata apenas de acertar o alvo.";
            next;
            mes "É sobre respeitar o ciclo da natureza.";
            mes "Há uma criatura chamada Willow.";
            mes "Quero que cace dez delas.";
            mes "Considere isso um teste de suas habilidades e paciência.";
            mes "Não volte até que tenha completado a tarefa.";
            setquest 75002;
        } else if (checkquest(75002) == 1 && Willow_Count >= 10) {
            mes "[Instrutor]";
			mes "Você fez bem. Não esperava menos."; 
			mes "Mas a jornada de um arqueiro não termina aqui.";
			mes "Há mais para aprender, e há alguém que pode te ensinar o próximo passo.";
			next;
			mes "Procure por Kaelen, o Rastreador, nos arredores de Payon.";
			mes "Ele é tão habilidoso quanto imprevisível.";
			mes "Se passar pelo teste dele, estará no caminho certo.";
            completequest 75002;
			erasequest(75002);
			set Willow_Count, Willow_Count == 0;
			getitem 1750, 500;
			getitem 1755, 500;

        }else if (checkquest(75002) == 1) {
            mes "[Instrutor]";
            mes "Ainda está trabalhando no teste?";
            mes "Não se esqueça: quero que cace Willows para demonstrar suas habilidades.";
			emotion ET_GO;
        } 
		close;
	}else{
		mes "Seja lá o que você for, não tenho nada para te ensinar.";
		close;
	}

	OnNPCKillEvent:	
    if (checkquest(75002) == 1 && killedrid == 1010 && Willow_Count < 10) {
        set Willow_Count, Willow_Count + 1;
        dispbottom "Willow derrotados: " + Willow_Count + "/10.";
    }else if (checkquest(75002) == 1 && killedrid == 1010 && Willow_Count >= 10){
		dispbottom "Você completou a missão de caçar 10 Willow!";	
	}
    end;
}


// gsmi , gnor , id 634 , arqueiro com ilustração 